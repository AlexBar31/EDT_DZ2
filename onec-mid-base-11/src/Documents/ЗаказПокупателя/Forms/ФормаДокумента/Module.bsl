
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
		
	//Начало_БАЮ:  
	БАЮ_ПолеКонтактноеЛицо();
	БАЮ_ДобавитьСогласованнаяСкидкаИКнопка();
   //Конец_БАЮ
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	//КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	//БАЮ_Начало: Задача 2
	КоэффициентСкидки = Объект.БАЮ_СогласованнаяСкидка + ТекущиеДанные.Скидка;
	Если КоэффициентСкидки > 100 Тогда 
		КоэффициентСкидки = 0;
		ОбщегоНазначенияКлиент.СообщитьПользователю("Суммарная скидка превысила 100%, к расчету принято скидка равная 100%!");
	Иначе 
		КоэффициентСкидки =  1 - (КоэффициентСкидки / 100);
	КонецЕсли;
	//Конец_БАЮ 
	
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти


//Начало_БАЮ:  
#Область БАЮ_СлужебныеПроцедурыИФункции

&НаСервере
 Процедура БАЮ_ПолеКонтактноеЛицо()
	
	ПолеКонтактноеЛицо = Новый Структура;
	ПолеКонтактноеЛицо.Вставить("Имя","БАЮ_КонтактноеЛицо");
	ПолеКонтактноеЛицо.Вставить("Заголовок","Контактное лицо");
	ПолеКонтактноеЛицо.Вставить("Тип",Тип("ПолеФормы"));
	ПолеКонтактноеЛицо.Вставить("Вид",ВидПоляФормы.ПолеВвода);
	ПолеКонтактноеЛицо.Вставить("ПутьКДанным","Объект.БАЮ_КонтактноеЛицо");
	БАЮ_РаботаСЭлементамиФормыСервер.БАЮ_ДобавлениеПоляФормы(ПолеКонтактноеЛицо,Элементы.ГруппаШапкаПраво, ЭтотОбъект);
КонецПроцедуры


&НаСервере
 Процедура БАЮ_ДобавитьСогласованнаяСкидкаИКнопка()
	//группа для поля и кнопки
	ГруппаСкидка = Новый Структура;
	ГруппаСкидка.Вставить("Имя","ГруппаСкидка");
	ГруппаСкидка.Вставить("Заголовок","");
	ГруппаСкидка.Вставить("Тип",Тип("ГруппаФормы"));
	ГруппаСкидка.Вставить("Вид",ВидГруппыФормы.ОбычнаяГруппа);
	ГруппаСкидка.Вставить("ОтображатьЗаголовок",Ложь);
	ГруппаСкидка.Вставить("Группировка",ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
	БАЮ_РаботаСЭлементамиФормыСервер.БАЮ_ДобавлениеГруппыФормы(ГруппаСкидка,
										Элементы.ГруппаШапкаЛево,ЭтотОбъект);
	 
	//поле скидка 
	ПолеСкидка = Новый Структура;
	ПолеСкидка.Вставить("Имя","БАЮ_СогласованнаяСкидка");
	ПолеСкидка.Вставить("Заголовок","Согласованная скидка");
	ПолеСкидка.Вставить("Тип",Тип("ПолеФормы"));
	ПолеСкидка.Вставить("Вид",ВидПоляФормы.ПолеВвода);
	ПолеСкидка.Вставить("ПутьКДанным","Объект.БАЮ_СогласованнаяСкидка");
	БАЮ_РаботаСЭлементамиФормыСервер.БАЮ_ДобавлениеПоляФормы(ПолеСкидка,Элементы.ГруппаСкидка,ЭтотОбъект);
	Элемент = Элементы.БАЮ_СогласованнаяСкидка;
	Элемент.УстановитьДействие("ПриИзменении","БАЮ_СогласованнаяСкидкаПриИзменении");
	
	
	//создаем команду 
	Команда = Команды.Добавить("БАЮ_ОбновитьТЧ");
	Команда.Заголовок = "Обновить ТЧ";
	Команда.Действие ="БАЮ_КомандаОбновитьТЧ";
	
	//создаем кнопку для команды
	Кнопка = Элементы.Добавить("БАЮ_КнопкаОбновитьТЧ",Тип("КнопкаФормы"),Элементы.ГруппаСкидка);
	Кнопка.ИмяКоманды = "БАЮ_ОбновитьТЧ";
	Кнопка.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
КонецПроцедуры    


 &НаКлиенте 
 Процедура БАЮ_КомандаОбновитьТЧ(Команда)  
	 
	 БАЮ_ОбновитьТЧДокумента();
	 
 КонецПроцедуры
 
 &НаКлиенте
 Процедура БАЮ_ОбновитьТЧДокумента()
	 
	 БАЮ_ОбновитьПоСкидкеТЧ(Объект.Товары);
	 БАЮ_ОбновитьПоСкидкеТЧ(Объект.Услуги);
	 РассчитатьСуммуДокумента();

КонецПроцедуры
 
 &НаКлиенте
 Процедура БАЮ_ОбновитьПоСкидкеТЧ(Таблица)
	 
	 Для Каждого Строка Из Таблица Цикл
		 РассчитатьСуммуСтроки(Строка);
	 КонецЦикла;
 КонецПроцедуры 
 
 &НаКлиенте
 Асинх Процедура БАЮ_СогласованнаяСкидкаПриИзменении(Элемент)  
	 
	 Если Объект.Товары.Количество() = 0 И Объект.Услуги.Количество() = 0 Тогда
		 Возврат;
	 КонецЕсли;
	 
	 Ответ = Ждать ВопросАсинх("Изменилась скидка! Пересчетать табличные части документа?",
	                            РежимДиалогаВопрос.ДаНет);
	 
	 Если Ответ = КодВозвратаДиалога.Нет Тогда
		 Возврат;
	 КонецЕсли;
	 
	 БАЮ_ОбновитьТЧДокумента();
 КонецПроцедуры
 


#КонецОбласти
//Конец_БАЮ

#КонецОбласти
